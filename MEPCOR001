#include 'protheus.ch'
#include 'parmtype.ch'
#include 'TopConn.ch'

/*/{Protheus.doc}

@author Luiz Neto
@since 16/08/2021
@Relatório diário - Geração de Pedido de Compras com visão orçamentária
@version undefined
@param nlOpc, numeric, Opcao a ser executada
@param uParam, undefined, Parametros da rotina a ser executada
@type function
/*/

user function MEPCOR001()

Private cVencrea1:= Space(8)
Private cVencrea2 := Space(8)


getParam()

FWMsgRun(, {|oSay| Processa() }, "Buscando Informações de Títulos a Pagar", "Gerando Excel" )

Return

Static Function Processa()

Local cQuery	:= ""
Local cAlias	:= GetNextAlias()
Local aDados	:= {}

Sleep(5000)

cQuery	:= "SELECT E2_FILIAL,M0_FILIAL, E2_PREFIXO, E2_NUM, E2_TIPO, E2_FORNECE, E2_NOMFOR, E2_HIST, E2_EMISSAO, E2_VENCREA,E2_VALOR,E2_CCUSTO, CTT_DESC01,E2_ZZCO, AK5_DESCRI	"+CRLF
cQuery	+= "FROM " + RetSQLName("SE2") + " SE2									                                                                      							"+CRLF
cQuery  += "INNER JOIN SYS_COMPANY SY ON E2_FILIAL = SY.M0_CODFIL AND SY.D_E_L_E_T_ = ''                                                      									"+CRLF
cQuery  += "LEFT JOIN " + RetSqlName("CTT") + " CTT ON CTT_CUSTO = E2_CCUSTO AND CTT.D_E_L_E_T_=''                                                       		   			  		"+CRLF
cQuery  += "LEFT JOIN " + RetSqlName("AK5") + " AK5 ON AK5_CODIGO = E2_ZZCO AND AK5.D_E_L_E_T_=''                                                         						"+CRLF
cQuery	+= "WHERE SE2.D_E_L_E_T_ = ' '											    																						    "+CRLF
cQuery	+= "AND E2_VENCREA >= '" + DTOS(cVencrea1) + "' AND  E2_VENCREA<= '" + DTOS(cVencrea2) + "'	    																		"+CRLF
cQuery	+= "AND E2_TIPO <> 'PR'																																					"+CRLF
cQuery  += "AND E2_ORIGEM IN ('FINA050', 'CNTA120', 'CNTA121')																													"+CRLF
cQuery	+= "ORDER BY E2_FILIAL, E2_NUM, E2_VENCREA																																"+ CRLF


TCQUERY cQuery NEW ALIAS (cAlias)

(cAlias)->(DbGoTop())

Do While !(cAlias)->(Eof())
					
	aAdd(aDados,{(cAlias)->E2_FILIAL,;
	(cAlias)->M0_FILIAL,;
    (cAlias)->E2_PREFIXO,;
    (cAlias)->E2_NUM,;
    (cAlias)->E2_TIPO,;
    (cAlias)->E2_FORNECE,;
    (cAlias)->E2_NOMFOR,;
    (cAlias)->E2_HIST,;
    (cAlias)->E2_EMISSAO,;
    (cAlias)->E2_VENCREA,;
    (cAlias)->E2_VALOR,;
    (cAlias)->E2_CCUSTO,;
    (cAlias)->CTT_DESC01,;
	(cAlias)->E2_ZZCO,;
	(cAlias)->AK5_DESCRI,;
	.F.})    

	(cAlias)->(DbSkip())

EndDo

(cAlias)->(DbCloseArea())

geraexcel(aDados)

return

Static Function geraexcel(aDados)

	Local oExcel 	:= FWMSExcel():New()
	Local oExcelApp	:= Nil
	Local cAba		:= "relacao diário de Titulos a pagar"
	Local cTabela	:= "Titulos a pagar"
	Local cArquivo	:= "Titulos a pagar" + dToS( msDate() ) + "_" + strtran(time(), ":", "") + ".XLS"
	Local cPath		:= "C:\TEMP\"
	Local cDefPath	:= GetSrvProfString( "StartPath", "\system\" )
	Local i
	
	If len(aDados) >0
	
		If !ApOleClient("MSExcel")
		    MsgAlert("Microsoft Excel não instalado!")
		    Return
		EndIf

		oExcel:AddWorkSheet(cAba)
		oExcel:AddTable(cAba, cTabela)
		
		oExcel:AddColumn(cAba, cTabela, "CODIGO FILIAL"				, 1, 1, .F.)
		oExcel:AddColumn(cAba, cTabela, "NOME FILIAL"				, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "PREFIXO"					, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "NUM TITULO"				, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "TIPO"						, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "CODIGO"					, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "NOME FORNECEDOR"			, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "HISTORICO"					, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "EMISSAO"					, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "VENC REAL"					, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "VALOR"						, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "C.C."						, 1, 1, .F.)
        oExcel:AddColumn(cAba, cTabela, "DESC C.C."					, 1, 1, .F.)
		oExcel:AddColumn(cAba, cTabela, "C.O."					, 1, 1, .F.)
		oExcel:AddColumn(cAba, cTabela, "DESC C.O."					, 1, 1, .F.)

		For i := 1 to len(aDados)

			oExcel:AddRow(cAba,;
			 			cTabela,;
					  		 {aDados[i][1]   	,;
					  		 aDados[i][2]	  	,;
							 aDados[i][3]   	,;
					  		  aDados[i][4]  	,;
					  		  aDados[i][5]   	,;
					  		  aDados[i][6]   	,;
                              aDados[i][7] ,;
                              aDados[i][8]      ,;
							  STOD(aDados[i][9])      ,;
							  STOD(aDados[i][10])      ,;
                              aDados[i][11]      ,;
                              aDados[i][12]      ,;
							  aDados[i][13]      ,;
							  aDados[i][14]      ,;
					  		  aDados[i][15]    	 })

		Next i

		If !Empty(oExcel:aWorkSheet)
		
		    oExcel:Activate()
		    oExcel:GetXMLFile(cArquivo)
		 
		    CpyS2T(cDefPath+cArquivo, cPath)
		
		    oExcelApp := MsExcel():New()
		    oExcelApp:WorkBooks:Open(cPath+cArquivo) // Abre a planilha
		    oExcelApp:SetVisible(.T.)
		
		EndIf 
	Else
		Alert("Não existem dados para emitir o relatório.")
	EndIf

Return

/*/{Protheus.doc} getParam
Funcao para controlar os parametros do Processamento
@author Luiz Neto
@since 16/08/2021
@version 1.0

@type function
/*/
Static Function getParam()

Local alParamBox	:= {}
Local clTitulo		:= "Parâmetros"
Local alButtons		:= {}
Local llCentered	:= .T.
Local nlPosx		:= Nil
Local nlPosy		:= Nil
Local clLoad		:= ""
Local llCanSave		:= .T.
Local llUserSave	:= .T.
Local llRet			:= .T.
Local blOk
Local alParams		:= {}
AADD(alParamBox,{1,"Venc de"		,Ctod(space(8))			,"@!",".T."     ,""   ,"",50,.F.})
AADD(alParamBox,{1,"Venc ate"	,Ctod(space(8))			,"@!",".T."     ,""   ,"",50,.F.})


llRet := ParamBox(alParamBox, clTitulo, alParams, blOk, alButtons, llCentered, nlPosx, nlPosy,, clLoad, llCanSave, llUserSave)

if ( llRet )
	cVencrea1    	:= alParams[1]
	cVencrea2   	    := alParams[2]
else
	Msginfo("Operacao Cancelada")
	RETURN .F.
Endif

Return

//teste
